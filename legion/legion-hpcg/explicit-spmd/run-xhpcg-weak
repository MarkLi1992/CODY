#!/usr/bin/env perl

###############################################################################
# Copyright (c) 2012-2017 Los Alamos National Security, LLC.
#                         All rights reserved.
#
# This program was prepared by Los Alamos National Security, LLC at Los Alamos
# National Laboratory (LANL) under contract No. DE-AC52-06NA25396 with the U.S.
# Department of Energy (DOE). All rights in the program are reserved by the DOE
# and Los Alamos National Security, LLC. Permission is granted to the public to
# copy and use this software without charge, provided that this Notice and any
# statement of authorship are reproduced on all copies. Neither the U.S.
# Government nor LANS makes any warranty, express or implied, or assumes any
# liability or responsibility for the use of this software.
################################################################################

use strict;
use warnings;

use Switch;
use Cwd qw(abs_path getcwd);
use File::Spec::Functions;
use File::Basename;
use Text::Wrap;
use POSIX qw(strftime);

my $SCRIPT_VER = '0.1';

################################################################################
sub usage
{
    print "\nPlease see README for environment setup instructions.\n";
}

################################################################################
sub env_def
{
    my ($env) = @_;

    return ($ENV{$env});
}

################################################################################
sub echo_setup
{
    my (%setup) = @_;

    print "# setup begin\n";
    foreach my $var_name (sort keys %setup) {
        print "# $var_name: $setup{$var_name}\n";
    }
    print "# setup end\n";
}

################################################################################
sub echo_run_info
{
    print "# what: " . basename($0) . " $SCRIPT_VER" . "\n";
    print "# who: " . $ENV{'USER'} . "\n";
    print "# when: " . strftime('%Y%m%d-%H%M%S', localtime) . "\n";

}

################################################################################
sub which_rxhpcg
{
    my ($exec_name) = @_;

    my $which_rxhpcg = undef;

    $which_rxhpcg = `which $exec_name 2>/dev/null`;

    if (!$which_rxhpcg) {
        # check if it is in the same directory as this script
        my $script_abs_dir_name = dirname(abs_path($0));
        my $mmu_test_path = catfile($script_abs_dir_name, $exec_name);
        if (-x $mmu_test_path) {
            return $mmu_test_path;
        }
        else {
            my $err = "Cannot find '$exec_name'. " .
                      "Please define RXHPCG_BIN_PATH, " .
                      "or add the location of $exec_name to your PATH.\n";
            print wrap('', '', $err) . "\n";
            exit(1);
        }
    }
    else {
        return $which_rxhpcg;
    }
}

################################################################################
sub make_datadir
{
    my ($exec_name, $dataprefix) = @_;

    my $datadir = $exec_name . '-' . $ENV{'USER'} . '-' .
                  strftime('%Y%m%d', localtime);
    my $fullpath = catfile($dataprefix, $datadir);
    my $i = 1;
    my $lock_file = undef;

    # XXX a race condition here. use file locks if need be.
    while (-x $fullpath) {
        $fullpath = catfile($dataprefix, "$datadir-$i");
        $i++;
    }
    mkdir($fullpath) or die "Fatal Error: Cannot create data directory: $!\n";
    # now add a lock file within the data dir. protect against some other job
    # from dumping data into this directory.
    $lock_file = catfile($fullpath, ".lock");
    # did another job lock this directory?
    if (-x $lock_file) {
        my $err = "Foreign lockfile found in $fullpath. Cannot continue.\n";
        print wrap('', '', $err) . "\n";
        exit(1);
    }
    # create the lock file with our pid
    open(LOCKFILE, ">", $lock_file) or die "Cannot create lock file: $!\n";
    print LOCKFILE "$$\n";
    close(LOCKFILE) or die "Cannot close lock file: $!\n";
    return $fullpath;
}

################################################################################
sub env_or_def
{
    my (@env_def) = @_;
    #
    my $env_var_name = $env_def[0];
    my $default_val  = $env_def[1];
    #
    if (env_def($env_var_name)) {
        return $ENV{$env_var_name};
    }
    else {
        # if not, set to default value
        return $default_val;
    }
}

################################################################################
sub get_setup
{
    my (@env_array) = @_;
    my %env_hash = ();

    @env_hash{@env_array} = undef;

    foreach my $env (@env_array) {
        switch ($env) {
            # Required environment variables.
            case ['RXHPCG_MAX_SUBBLOCKS',
                  'RXHPCG_RUN_CMD',
                  'RXHPCG_NUMPE_FUN',
                  'RXHPCG_EXEC_NAME'] {
                if (env_def($env)) {
                    $env_hash{$env} = $ENV{$env};
                }
                else {
                    print "$env not defined. Cannot continue.\n";
                    usage();
                    exit(1);
                }
            }
            case 'RXHPCG_START_INDEX' {
                $env_hash{$env} = env_or_def(($env, '0'));
            }
            case 'RXHPCG_PPN' {
                $env_hash{$env} = env_or_def(($env, '1'));
            }
            case 'RXHPCG_DATA_DIR_PREFIX' {
                $env_hash{$env} = env_or_def(($env, getcwd));
            }
            case 'RXHPCG_BIN_PATH' {
                my $exec_name = $env_hash{'RXHPCG_EXEC_NAME'};
                $env_hash{$env} = env_or_def(($env, which_rxhpcg($exec_name)));
            }
            case ['RXHPCG_NX', 'RXHPCG_NY', 'RXHPCG_NZ'] {
                $env_hash{$env} = env_or_def(($env, '16'));
            }
            case 'RXHPCG_RT' {
                $env_hash{$env} = env_or_def(($env, '1'));
            }
        }
    }
    return %env_hash;
}

################################################################################
sub get_real_run_cmd
{
    my ($run_cmd, $numpe, $ppn, $app) = @_;

    # make sure that the string we are working with has all the required
    # variables. don't include NNN, because they may not want to use that
    # switch. nnn and aaa are required.
    if (!($run_cmd =~ /nnn/ and $run_cmd =~ /aaa/)) {
        die "Invalid run command provided. Must contain nnn and aaa " .
            "variables.\n";
    }
    # substitute the placeholders with the real values
    if ($run_cmd =~ /NNN/) {
        $run_cmd =~ s/NNN/$ppn/;
        if (!env_def('RXHPCG_PPN')) {
            print "# WARNING: 'NNN' provided, but RXHPCG_PPN not set. " .
                  "RXHPCG_PPN set to 1.\n";
        }
    }
    $run_cmd =~ s/nnn/$numpe/;
    $run_cmd =~ s/aaa/$app/;

    return $run_cmd;
}

################################################################################
# returns an array of the numpes that will be used during the run.
sub get_numpes
{
    my ($starti, $numpefun, $maxnumpe) = @_;
    my $i = $starti;
    my $cur_numpe = 0;
    my @numpes = ();
    my $cur_math_expr = $numpefun;

    if (!($numpefun =~ /X/)) {
        my $err = "Invalid RXHPCG_NUMPE_FUN format provided - " .
                  "missing 'X' variable. Cannot continue.\n";
        print wrap('', '', $err) . "\n";
        exit(1);
    }
    #
    my $x_count = () = $numpefun =~ /X/g;
    if ($x_count > 1) {
        my $err = "Invalid RXHPCG_NUMPE_FUN format provided - " .
                  "more than one 'X' variable found. Cannot continue.\n";
        print wrap('', '', $err) . "\n";
        exit(1);
    }
    #
    $cur_math_expr =~ s/X/$i/;
    $cur_numpe = eval($cur_math_expr);

    while ($cur_numpe <= $maxnumpe) {
        push(@numpes, $cur_numpe);
        $i++;
        $cur_math_expr = $numpefun;
        $cur_math_expr =~ s/X/$i/;
        $cur_numpe = eval($cur_math_expr);
    }
    # remove 0 if present
    if (@numpes && $numpes[0] == 0) {
        splice(@numpes, 0, 1);
    }
    # make sure that we have maxnumpe in the array
    if ((! @numpes) || $numpes[-1] != $maxnumpe) {
        push(@numpes, $maxnumpe);
    }
    return @numpes;
}

################################################################################
sub run
{
    my %setup = @_;

    my $maxnumpe = $setup{'RXHPCG_MAX_SUBBLOCKS'};
    my $ppn = $setup{'RXHPCG_PPN'};
    my $numpefun = $setup{'RXHPCG_NUMPE_FUN'};
    my $starti = $setup{'RXHPCG_START_INDEX'};

    my $app_str = "$setup{'RXHPCG_BIN_PATH'} " .
                  "--nx=$setup{'RXHPCG_NX'} "  .
                  "--ny=$setup{'RXHPCG_NY'} "  .
                  "--nz=$setup{'RXHPCG_NZ'} "  .
                  "--rt=$setup{'RXHPCG_RT'}";

    my $datadir = make_datadir(
                      $setup{'RXHPCG_EXEC_NAME'},
                      $setup{'RXHPCG_DATA_DIR_PREFIX'}
                  );
    my $num_runs = 0;
    my @numpes = ();

    @numpes = get_numpes($starti, $numpefun, $maxnumpe);
    for my $numpe (@numpes) {
        my $cmd = get_real_run_cmd($setup{'RXHPCG_RUN_CMD'}, $numpe,
                                   $setup{'RXHPCG_PPN'},
                                   $app_str);

        my $log = catfile($datadir, $numpe);
        print wrap('', '', "\n# running: $cmd\n");
        # run the command and capture its output
        unless (open(OUTFILE, "$cmd 2>&1 |")) {
            die "Fatal Error: Cannot spawn $cmd!\n";
        }
        unless (open(LOGFILE, ">$log")) {
            die "Fatal Error: Cannot open LOGFILE\n";
        }
        while (defined(my $out_line = <OUTFILE>)) {
            print $out_line;
            print LOGFILE $out_line;
        }
        unless (close(OUTFILE)) {
            die "Fatal Error: $!\n";
        }
        unless (close(LOGFILE)) {
            die "Fatal Error: $!\n";
        }
        $num_runs++;
        sleep(1);
    }
    print "\n# done with $num_runs run(s).\n";
    print "# data written to: $datadir\n\n";
}

################################################################################
sub main
{
    # environment variables that dictate how this script behaves.
    my @rxhpcg_envs = (
        'RXHPCG_START_INDEX',
        'RXHPCG_MAX_SUBBLOCKS',
        'RXHPCG_PPN',
        'RXHPCG_RUN_CMD',
        'RXHPCG_NUMPE_FUN',
        'RXHPCG_DATA_DIR_PREFIX',
        'RXHPCG_EXEC_NAME',
        'RXHPCG_BIN_PATH',
        'RXHPCG_NX',
        'RXHPCG_NY',
        'RXHPCG_NZ',
        'RXHPCG_RT'
    );
    # application settings hash table
    my %setup_hash = ();
    # set setup and also check if all the required envs are set.
    %setup_hash = get_setup(@rxhpcg_envs);
    # if we are here, then we have a good setup
    echo_run_info();
    echo_setup(%setup_hash);
    # run the thing
    run(%setup_hash);
    #
    return 0;
}

################################################################################
################################################################################
exit(main());
